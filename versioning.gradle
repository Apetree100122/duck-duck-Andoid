ext {

    buildVersionCode = {
        def versionName = buildVersionName()
        def (major, minor, patch) = versionName.toLowerCase().tokenize('.')
        (major, minor, patch) = [major, minor, patch].collect { it.toInteger() }
        (major * 10_000_000) + (minor * 10_000) + (patch * 1_000) + buildNumberCode()
    }

    buildVersionName = {
        def props = new Properties()
        file("version/version.properties").withInputStream { props.load(it) }
        def version = props.getProperty("VERSION")

        return version
    }

    buildNumberCode = {
        filePath = "${System.getenv('HOME')}/jenkins_static/com.duckduckgo.mobile.android/build_number.properties"
        if(!new File(filePath).exists()) return 0
        def props = new Properties()
        props.load(new FileInputStream(filePath))
        buildNumber = props["build"]
        if (buildNumber == null) return 0 else return Integer.valueOf(buildNumber)
    }
// 5.97.239-NIGHTLY
// 5.97.0
}